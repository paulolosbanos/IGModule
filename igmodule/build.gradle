apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'

def githubProperties = new Properties()

if( rootProject.file("github.properties").exists() ) {
    githubProperties.load(new FileInputStream(rootProject.file("github.properties")))
}

def getVersionName = { ->
    return "1.0.3"
}

def getArtificatId = { ->
    return "igmodule" // Replace with library name ID
}

publishing {
    publications {
        release(MavenPublication) {
            groupId 'com.atlas.igmodule'
            artifactId getArtificatId()
            version getVersionName()
            artifact("igmodule/build/outputs/aar/${getArtificatId()}-release.aar")
        }

        debug(MavenPublication) {
            groupId 'com.atlas.igmodule'
            artifactId "IGModule-Debug"
            version "1.0.0"
            artifact("igmodule/build/outputs/aar/${getArtificatId()}-debug.aar")
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/paulolosbanos/IGModule")

            credentials {
                username = githubProperties['gpr.usr'] ?: System.getenv("GITHUB_ACTOR")
                password = githubProperties['gpr.key'] ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

}
